buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath "gradle.plugin.com.palantir.graal:gradle-graal:0.7.1"
        classpath "com.google.gradle:osdetector-gradle-plugin:1.6.2"
    }
}

apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: com.palantir.gradle.graal.GradleGraalPlugin
apply plugin: com.google.gradle.osdetector.OsDetectorPlugin

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.bsycorp'
if (findProperty("appVersion") != null){
    version = findProperty("appVersion")
}

dependencies {
    compile(project(":kees"))

    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.8.4"
    testCompile "com.github.stefanbirkner:system-rules:1.17.0"
}

artifacts {
    archives jar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true

    pkg {
        repo = 'public'
        name = 'kees-init'
        desc = "Kubernetes External Environment Secrets"
        userOrg = 'bsycorp'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bsycorp/kees.git'

        version {
            name = System.getProperty('appVersion')
            desc = 'Kubernetes External Environment Secrets'
            released = new Date()
            gpg {
                sign = true
            }
        }
    }

}

bintrayUpload.dependsOn(jar)

def nativeImageOutput = "kees-init-${osdetector.classifier}"
graal {
    mainClass 'com.bsycorp.kees.InitMain'
    outputName nativeImageOutput
    graalVersion "20.2.0"
    javaVersion JavaVersion.VERSION_1_8.majorVersion
    option '-H:IncludeResources=.*keys.json$'
//    option '-H:+PrintMethodHistogram'
//    option '-H:+PrintRuntimeCompileMethods'
//    option '-H:Log=registerResource:'
    option '-H:ReflectionConfigurationFiles=../base/src/main/resources/reflect-config.json'
    option '--enable-http'
    option '--enable-https'
    option '--allow-incomplete-classpath'
    option '--report-unsupported-elements-at-runtime'
    option '--install-exit-handlers'
    option '--no-server'
    option '--no-fallback'
    option '--verbose'
    option '-J-Dfile.encoding=UTF-8'
    option '-J-Xmx2500m' //bound memory so it is of a consistent size may need to increase over time
}